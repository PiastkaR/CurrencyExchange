plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.6'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'groovy'
}

group = 'com.nn'
version = '1.0-SNAPSHOT'
sourceCompatibility = '21'

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/release" }
}

bootJar {
    archiveFileName = 'CurrencyExchange.jar'
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output + sourceSets.test.output
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output
        groovy {
            srcDirs = ['src/integrationTest/groovy']
        }
        resources {
            srcDirs = ['src/integrationTest/resources']
        }
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.liquibase:liquibase-core'
    runtimeOnly 'org.postgresql:postgresql'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.spockframework:spock-core:2.4-M1-groovy-4.0'
    testImplementation 'com.github.tomakehurst:wiremock-jre8:2.35.1'
    integrationTestImplementation 'org.springframework.cloud:spring-cloud-starter-contract-stub-runner:4.0.5'
}

test {
    useJUnitPlatform()
}

task integrationTest(type: Test) {
    description = "Runs the integration tests."
    group = "verification"
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test
    useJUnitPlatform()
}

check.dependsOn integrationTest