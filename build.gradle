plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.6'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'groovy'
    id 'idea'

}

group = 'com.nn'
version = '1.0-SNAPSHOT'
sourceCompatibility = '21'

repositories {
    mavenCentral()
}

bootJar {
    archiveFileName = 'CurrencyExchange.jar'
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath + configurations.testCompileClasspath + sourceSets.test.output
        runtimeClasspath += output + compileClasspath + configurations.testRuntimeClasspath + sourceSets.test.output
    }
}

idea {
    module {
        testSourceDirs += sourceSets.integrationTest.java.srcDirs
        testSourceDirs += sourceSets.integrationTest.groovy.srcDirs
        testResourceDirs += sourceSets.integrationTest.resources.srcDirs
        scopes.TEST.plus += [configurations.integrationTestRuntimeClasspath, configurations.integrationTestCompileClasspath]
    }
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    mustRunAfter test
    useJUnitPlatform()
}

test {
    useJUnitPlatform()
}

check.dependsOn integrationTest

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.liquibase:liquibase-core'
    runtimeOnly 'org.postgresql:postgresql'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.spockframework:spock-core:2.4-M1-groovy-4.0'
    testImplementation 'org.spockframework:spock-spring:2.4-M1-groovy-4.0'
    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-stub-runner:4.0.5'
    testImplementation 'org.testcontainers:postgresql:1.20.4'

    integrationTestImplementation("org.springframework.boot:spring-boot-autoconfigure")
    integrationTestImplementation'org.springframework.cloud:spring-cloud-starter-contract-stub-runner:4.0.5'

}

